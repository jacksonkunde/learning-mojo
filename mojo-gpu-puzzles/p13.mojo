from gpu import thread_idx, block_idx, block_dim, barrier
from gpu.host import DeviceContext
from gpu.memory import AddressSpace
from layout import Layout, LayoutTensor
from sys import size_of, argv
from testing import assert_equal

# ANCHOR: conv_1d_simple
alias TPB = 8
alias SIZE = 6
alias CONV = 3
alias BLOCKS_PER_GRID = (1, 1)
alias THREADS_PER_BLOCK = (TPB, 1)
alias dtype = DType.float32
alias in_layout = Layout.row_major(SIZE)
alias out_layout = Layout.row_major(SIZE)
alias conv_layout = Layout.row_major(CONV)


fn conv_1d_simple[
    in_layout: Layout, out_layout: Layout, conv_layout: Layout
](
    output_vec: LayoutTensor[dtype, out_layout, MutAnyOrigin],
    input_vec: LayoutTensor[dtype, in_layout, ImmutAnyOrigin],
    input_conv: LayoutTensor[dtype, conv_layout, ImmutAnyOrigin],
):

    shared_vec = LayoutTensor[
        dtype,
        in_layout,
        MutAnyOrigin,
        address_space=AddressSpace.SHARED
    ].stack_allocation()

    shared_conv = LayoutTensor[
        dtype,
        conv_layout,
        MutAnyOrigin,
        address_space=AddressSpace.SHARED
    ].stack_allocation()

    # Set global_i
    global_i = block_idx.x * block_dim.x + thread_idx.x

    # set local_i
    local_i = thread_idx.x 

    # fill our shared memory
    if global_i < SIZE:
        shared_vec[local_i] = input_vec[global_i]

    if local_i < CONV:
        shared_conv[local_i] = input_conv[local_i]

    # sync threads
    barrier()

    # Now for the algo.
    if global_i < SIZE:
        # This says that the type is the same as
        # the type of output_vec
        # var means that it is mutable
        var local_sum: output_vec.element_type = 0
        @parameter
        for j in range(CONV):
            if local_i + j < SIZE:
                local_sum += shared_vec[local_i + j] * shared_conv[j]

        output_vec[global_i] = local_sum

    
                

 



# ANCHOR_END: conv_1d_simple

# ANCHOR: conv_1d_block_boundary
alias SIZE_2 = 15
alias CONV_2 = 4
alias BLOCKS_PER_GRID_2 = (2, 1)
alias THREADS_PER_BLOCK_2 = (TPB, 1)
alias in_2_layout = Layout.row_major(SIZE_2)
alias out_2_layout = Layout.row_major(SIZE_2)
alias conv_2_layout = Layout.row_major(CONV_2)


fn conv_1d_block_boundary[
    in_layout: Layout, out_layout: Layout, conv_layout: Layout, dtype: DType
](
    output_vec: LayoutTensor[dtype, out_layout, MutAnyOrigin],
    a: LayoutTensor[dtype, in_layout, ImmutAnyOrigin],
    b: LayoutTensor[dtype, conv_layout, ImmutAnyOrigin],
):
    # Set global_i
    global_i = block_idx.x * block_dim.x + thread_idx.x

    # set local_i
    local_i = thread_idx.x 

    # create shared memory
    # each thread needs to store TPB + CONV_2 - 1
    shared_vec = LayoutTensor[
        dtype,
        Layout.row_major(TPB + CONV_2 - 1),
        MutAnyOrigin,
        address_space=AddressSpace.SHARED
    ].stack_allocation()
    
    shared_conv = LayoutTensor[
        dtype,
        conv_layout,
        MutAnyOrigin,
        address_space=AddressSpace.SHARED
    ].stack_allocation()

    # load in the conv, within local thread block
    if local_i < CONV_2:
        shared_conv[local_i] = b[local_i]
    

    # copy a to shared memory
    # get yourself
    if global_i < SIZE_2:
        shared_vec[local_i] = a[global_i]
    # if you are the last thread, get the next CONV_2 - 1 elets too
    if local_i == TPB - 1:
        @parameter # unroll at compile time for efficency
        for j in range(1, CONV_2):
            shared_vec[local_i + j] = a[global_i + j]

    # sync threads
    barrier()

    # Now for the algo.
    if global_i < SIZE_2:
        # This says that the type is the same as
        # the type of output_vec
        # var means that it is mutable
        var local_sum: output_vec.element_type = 0
        @parameter
        for j in range(CONV_2):
            if local_i + j < SIZE_2:
                local_sum += shared_vec[local_i + j] * shared_conv[j]

        output_vec[global_i] = local_sum


# ANCHOR_END: conv_1d_block_boundary


def main():
    with DeviceContext() as ctx:
        size = SIZE_2 if argv()[1] == "--block-boundary" else SIZE
        conv = CONV_2 if argv()[1] == "--block-boundary" else CONV
        out = ctx.enqueue_create_buffer[dtype](size)
        out.enqueue_fill(0)
        a = ctx.enqueue_create_buffer[dtype](size)
        a.enqueue_fill(0)
        b = ctx.enqueue_create_buffer[dtype](conv)
        b.enqueue_fill(0)
        with a.map_to_host() as a_host:
            for i in range(size):
                a_host[i] = i

        with b.map_to_host() as b_host:
            for i in range(conv):
                b_host[i] = i

        if len(argv()) != 2 or argv()[1] not in [
            "--simple",
            "--block-boundary",
        ]:
            raise Error(
                "Expected one command-line argument: '--simple' or"
                " '--block-boundary'"
            )

        if argv()[1] == "--simple":
            var out_tensor = LayoutTensor[dtype, out_layout, MutAnyOrigin](out)
            var a_tensor = LayoutTensor[dtype, in_layout, ImmutAnyOrigin](a)
            var b_tensor = LayoutTensor[dtype, conv_layout, ImmutAnyOrigin](b)
            alias kernel = conv_1d_simple[in_layout, out_layout, conv_layout]
            ctx.enqueue_function_checked[kernel, kernel](
                out_tensor,
                a_tensor,
                b_tensor,
                grid_dim=BLOCKS_PER_GRID,
                block_dim=THREADS_PER_BLOCK,
            )
        else:
            var out_tensor = LayoutTensor[dtype, out_2_layout, MutAnyOrigin](
                out
            )
            var a_tensor = LayoutTensor[dtype, in_2_layout, ImmutAnyOrigin](a)
            var b_tensor = LayoutTensor[dtype, conv_2_layout, ImmutAnyOrigin](b)
            alias kernel = conv_1d_block_boundary[
                in_2_layout, out_2_layout, conv_2_layout, dtype
            ]
            ctx.enqueue_function_checked[kernel, kernel](
                out_tensor,
                a_tensor,
                b_tensor,
                grid_dim=BLOCKS_PER_GRID_2,
                block_dim=THREADS_PER_BLOCK_2,
            )

        ctx.synchronize()
        expected = ctx.enqueue_create_host_buffer[dtype](size)
        expected.enqueue_fill(0)

        with a.map_to_host() as a_host, b.map_to_host() as b_host:
            for i in range(size):
                for j in range(conv):
                    if i + j < size:
                        expected[i] += a_host[i + j] * b_host[j]

        with out.map_to_host() as out_host:
            print("out:", out_host)
            print("expected:", expected)
            for i in range(size):
                assert_equal(out_host[i], expected[i])